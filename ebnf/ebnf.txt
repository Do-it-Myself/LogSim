specfile = [deviceList], [clockList], [switchList], monitorList, [connectionList] ;

deviceList = "DEVICE", "{", device, {device}, "}" ;
clockList = "CLOCK", "{", clock, {clock}, "}" ;
switchList = "SWITCH", "{", switch, {switch}, "}" ;
monitorList = "MONITOR", "{", monitor, {monitor}, "}" ;
connectionList = "CONNECTION", "{", connection, {connection}, "}" ;

device = identifier, ":" ((variableInputDevice, ",", variableInputNumber) |
fixedInputDevice), ";" ;
clock = identifier, ":", clockCycle, ";" ;
switch = identifier, ":", initialState, ";" ;
monitor = identifier, ":", identifier, [ ".", ( pinIn | pinOut ) ], ";" ;
connection = identifier, [".", pinOut] , ">", identifier, ".", pinIn, ";" ;

variableInputDevice = "AND" | "NAND" | "OR" | "NOR" ;
fixedInputDevice = "XOR" | "DTYPE" ;
initialState = "0", "1" ;
pinIn = ( "I", variableInputNumber ) | "DATA" | "CLK" | "SET" | "CLEAR" ;
pinOut = "Q" | "QBAR";

identifier = letter , { letter | digit | "_" } ;
letter = "a" | "b" | ... | "z" | "A" | ... | "Z";

digitExcludingZero = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
digit = "0" | digitExcludingZero ;
clockCycle = digitExcludingZero, { digit } ;
variableInputNumber = "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" |
"10" | "11" | "12" | "13" | "14" | "15" | "16" ;


